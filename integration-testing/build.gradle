/*
 * Copyright 2016-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id "org.jetbrains.kotlin.jvm"
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    // Workaround to make addSuppressed work in tests
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

sourceSets {
    mavenTest {
        kotlin
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath

        dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
        }
    }
    debugAgentTest {
        kotlin
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath

        dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:$coroutines_version"
        }
    }
    coreAgentTest {
        kotlin
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath

        dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        }
    }
}

compileDebugAgentTestKotlin {
    kotlinOptions {
        freeCompilerArgs += ["-Xallow-kotlin-package"]
    }
}

task mavenTest(type: Test) {
    environment "version", coroutines_version
    def sourceSet = sourceSets.mavenTest
    testClassesDirs = sourceSet.output.classesDirs
    classpath = sourceSet.runtimeClasspath
}

task debugAgentTest(type: Test) {
    def sourceSet = sourceSets.debugAgentTest
    //dependsOn(project(':kotlinx-coroutines-debug').shadowJar)
    def debugJar = null
    sourceSet.runtimeClasspath.files.forEach {if (it.name.contains("kotlinx-coroutines-debug")) debugJar = it }
    jvmArgs ('-javaagent:' + debugJar)
    testClassesDirs = sourceSet.output.classesDirs
    classpath = sourceSet.runtimeClasspath
    systemProperties project.properties.subMap(["overwrite.probes"])
}

task coreAgentTest(type: Test) {
    def sourceSet = sourceSets.coreAgentTest
    def coreJar = null
    sourceSet.runtimeClasspath.files.forEach {if (it.name.contains("kotlinx-coroutines-core")) coreJar = it }
    jvmArgs ('-javaagent:' + coreJar)
    testClassesDirs = sourceSet.output.classesDirs
    classpath = sourceSet.runtimeClasspath
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

check {
    dependsOn([mavenTest, coreAgentTest, ':smokeTest:build'])
}
